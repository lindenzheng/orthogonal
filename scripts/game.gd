extends Control

# We need to:
# 1. Share the world of the first viewport with the second viewport.
# 2. Create a remote transform attached to each player that pushes their position to the camera.
# This data structure helps us to do that conveniently. See the _ready() function below.
@onready var players: Array[Dictionary] = [
	{
		sub_viewport = %LeftSubViewport,
		camera = %LeftCamera2D,
		player = %Level/Player1,
	},
	{
		sub_viewport = %RightSubViewport,
		camera = %RightCamera2D,
		player = %Level/Player2,
	},
]

# Called when the node enters the scene tree for the first time.
func _ready() -> void:
	# The `world_2d` object of the Viewport class contains information about
	# what to render. Here, it's our game level. We need to pass it
	# from the first to the second SubViewport for both of them to render
	# the same level.
	players[1].sub_viewport.world_2d = players[0].sub_viewport.world_2d

	# For each player, we create a remote transform that pushes the character's
	# position to the corresponding camera.
	for info in players:
		var remote_transform := RemoteTransform2D.new()
		remote_transform.remote_path = info.camera.get_path()
		info.player.add_child(remote_transform)


# Called every frame. 'delta' is the elapsed time since the previous frame.
func _process(_delta: float) -> void:
	pass
